/* --------------------  rexx procedure  -------------------- *
 * Name:      chkpdsem                                        *
 *                                                            *
 * Function:  Utility to run under ISPF (foreground or batch) *
 *            to report on all datasets that are PDSE V2      *
 *            with maxgen > 0                                 *
 *                                                            *
 * Syntax:    %chkpdsem volser                                *
 *                                                            *
 * Dependencies:                                              *
 *            1. The VTOC TSO Command for the list of dsns    *
 *               on the volume                                *
 *            2. ISPF services                                *
 *            2. Only checks cataloged datasets               *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * History:                                                   *
 *            07/19/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 arg volser

 if strip(volser) = '' then do
    say 'CHKPDSEM requires a volser to process.'
    say 'Try again .....'
    exit 16
    end

 if sysvar('sysispf') /= 'ACTIVE' then do
    say 'CHKPDSEM must be run under ISPF, foreground or batch'
    say 'Try again .....'
    exit 16
    end

 call outtrap 'vtoc.'
 'vtoc' volser 'print(new (dsname volume dso rfm))'
 call outtrap 'off'

 say 'Check for PDSE Version 2 with Maxgen > 0' date() time()
 say ' '
 say 'Checking on Volser:' volser
 say ' '
 say left('Dataset',46) 'MaxGen'
 say left('-',44,'-') '  ------'

 count = 0

 Address ISPExec

 do i = 2 to vtoc.0
    if word(vtoc.i,1) = 'TOTAL' then leave
    parse value vtoc.i with dsn volume dsorg recfm .
    if dsorg /= 'PE' then iterate
    if strip(recfm) = '' then iterate
    if recfm = 'U' then iterate
    "DSINFO dataset('"dsn"')"
    if zdsngen > 0 then do
       say left(dsn,46) strip(zdsngen)
       count = count + 1
       end
    end

 Say ' '
 say 'Processing completed - ' count 'datasets found with maxgen > 0'

 Exit 0
