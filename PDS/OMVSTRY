  /* --------------------  rexx procedure  -------------------- *
  | Name:      omvstry                                         |
  |                                                            |
  | Function:  allow the tso user to execute omvs shell        |
  |            commands and see the results in a non-ISPF,     |
  |            non-shell, interface.                           |
  |                                                            |
  | Syntax:    %omvstry                                        |
  |                                                            |
  | Usage Notes: 1) not intended to be run under OMVS or Shell |
  |              2) any OMVS/shell command accepted            |
  |              3) terminate with the command 'exit'          |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            2025/03/27 LBD - Refine and add comment         |
  |            2025/03/26 LBD - Creation                       |
  |                                                            |
  * ---------------------------------------------------------- */
  /* ----------------------------- *
  | Get the user's home directory |
  * ----------------------------- */
  address syscall 'getpwnam' sysvar('sysuid') 'h.'
  env.1 = 'HOME='h.4
  env.0 = 1
  login = 1
  /* ------------------------------------ *
  | Get the active environment variables |
  * ------------------------------------ */
  rc = bpxwunix('env',,stdout.,stderr.,env.,login)
  /* -------------------------- *
  | Now populate the env. stem |
  * -------------------------- */
  c = 0
  do i = 1 to stdout.0
    if pos('=',stdout.i) = 0 then iterate
    c = c + 1
    env.c = stdout.i
  end
  env.0 = c

  /* --------------------------------------------- *
  | Now prompt for any OMVS shell command or EXIT |
  * --------------------------------------------- */
  do forever
    say 'Enter any OMVS/shell command or Exit to terminate'
    parse pull cmd
    if translate(cmd) = 'EXIT' then exit
    drop stdout. stderr.
    rc = bpxwunix(cmd,,stdout.,stderr.,env.)
    /* ------------------------------------ *
    | Display stdout. and stderr. messages |
    * ------------------------------------ */
    say ' '
    do i = 1 to stdout.0;say stdout.i;end
    say ' '
    if stderr.0 > 0 then do
      say ' '
      say 'stderr:'
      say ' '
      do i = 1 to stderr.0;say stderr.i;end
    end
  end
