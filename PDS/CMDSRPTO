        /* ------------------------ rexx --------------------------- *
         * Name:  CMDSRPT                                            *
         *                                                           *
         * Function:  Report the specified ISPF Commands Table       *
         *                                                           *
         * Syntax:   %CMDSRPT   table-name                           *
         *                                                           *
         * table-name must end in CMDS (or it isn't a command table) *
         *                                                           *
         * Notes:                                                    *
         * 1. The table will be sorted by the verb (aka Command)     *
         *    before it is reported out                              *
         * 2. The report will be opened in ISPF Edit                 *
         * --------------------------------------------------------- */
        arg table
        if length(table) = 0 then exit 16
        if right(table,4) /= "CMDS" then exit 16

        Address ISPExec
        "TBTop" table
        "TBSort" table "fields(zctverb,c,a)"
        cnt = 0
        do forever
          "TBSkip" table
          if rc > 0 then leave
          cnt = cnt + 1
          t.cnt = left(zctverb,10) left(zcttrunc,3) zctact
          cnt = cnt + 1
          t.cnt =  left(' ',14) zctdesc
          end
          t.0 = cnt

          Address TSO
          cmd_dd = "C"random()
          "Allocate File("cmd_dd") Reuse Unit(vio) Space(1 1)" ,
                   "CYL Dsorg(ps) Recfm(v b) Lrecl(255)"
          "Execio * Diskw" cmd_dd "(Stem t. Finis)"
          Address ISPExec
          "LMinit Dataid(dataid) DDname("cmd_dd")"
          "Edit Dataid("dataid")"
          "LMfree Dataid("dataid")"
          Address TSO
          "Free File("cmd_dd")"
