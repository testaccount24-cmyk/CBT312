        /* ------------------------ rexx --------------------------- *
         * Name:  CMDSRPT                                            *
         *                                                           *
         * Function:  Report the specified ISPF Commands Table       *
         *                                                           *
         * Syntax:   %CMDSRPT   table-name csv                       *
         *                                                           *
         * table-name must end in CMDS (or it isn't a command table) *
         *                                                           *
         * csv is optional and may be any non-blank character. If    *
         * specified then the report will be in CSV format.          *
         *                                                           *
         * Notes:                                                    *
         * 1. The table will be sorted by the verb (aka Command)     *
         *    before it is reported out                              *
         * 2. The report will be opened in ISPF Edit                 *
         * 3. The CSV report file dataset name is a variable which   *
         *    can be changed in the code                             *
         * 4. Download the CSV file or e-mail it to open in a        *
         *    spreadsheet tool.                                      *
         * --------------------------------------------------------- */
        arg table csv .
        if length(table) = 0 then exit 16
        if right(table,4) /= "CMDS" then exit 16

        if csv /= '' then do
           csv = "CSV"
           csvdsn = "command."table".csv"
           end

        Address ISPExec
        "TBTop" table
        "TBSort" table "fields(zctverb,c,a)"
        cnt = 0
        if csv /= '' then do
           cnt = 1
           t.cnt = "Verb,Trunc,Action,Description"
           end
        do forever
          "TBSkip" table
          if rc > 0 then leave
          if csv = '' then do
             cnt = cnt + 1
             t.cnt = left(zctverb,10) left(zcttrunc,3) zctact
             cnt = cnt + 1
             t.cnt =  left(' ',14) zctdesc
             end
          else do
               cnt = cnt + 1
               t.cnt = zctverb','zcttrunc',"'zctact'","'strip(zctdesc)'"'
               end
          end
          t.0 = cnt

          Address TSO
          cmd_dd = "C"random()
          if csv = '' then
          "Allocate File("cmd_dd") Reuse Unit(vio) Space(1 1)" ,
                   "CYL Dsorg(ps) Recfm(v b) Lrecl(255)"
           else do
           call outtrap 'x.'
           "Del" csvdsn
           call outtrap 'off'
          "Allocate File("cmd_dd") Reuse Unit(sysallda) Space(1 1)" ,
                   "CYL Dsorg(ps) Recfm(v b) Lrecl(255)" ,
                   "DA("csvdsn")"
                   end
          "Execio * Diskw" cmd_dd "(Stem t. Finis)"
          Address ISPExec
          "LMinit Dataid(dataid) DDname("cmd_dd")"
          "Edit Dataid("dataid")"
          "LMfree Dataid("dataid")"
          Address TSO
          "Free File("cmd_dd")"
