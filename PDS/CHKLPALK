/* ---------------------  rexx procedure  ---------------------- */

parse value "" with null dsn. lpa. lpaflag

dsn.0 = 11
dsn.1 = "-----LPA and LINKLIST Report-----"
dsn.2 = "   "
dsn.3 = "System:" mvsvar('sysname')  "Date:" date('u') time()
dsn.4 = "   "
dsn.5 = ">  **2** indicates a Secondary Allocation"
dsn.6 = ">  Ext: xx indicates using xx extents"
dsn.7 = ">  lpa: if link list data set also in lpa"
dsn.8 = "   "
dsn.9 = "System LPA List"
dsn.10 = "---------------------------------------------------------"
dsn.11 = left("Data Set Name",50) "Volser"

/* REXX - List the current LPALST */
@CVT    = STORAGE(10,4)                      /* L R1,CVTPTR             */
@SMEXT  = STORAGE(D2X(C2D(@CVT)+1196),4)     /* L R1,CVTSMEXT-CVTMAP(R1 */
@EPLPS  = STORAGE(D2X(C2D(@SMEXT)+56),4)     /* L R1,CVTEPLPS-CVTVSTGX( */
LPAC    = C2D(STORAGE(D2X(C2D(@EPLPS)+4),4)) /* L R1,LPATCNT(R1)     */
@EPLPS  = D2X(C2D(@EPLPS)+8)                 /* LA R1,LPATNTRY(R1)      */
do i = 1 TO LPAC
   ddw = STORAGE(D2X(X2D(@EPLPS)+1),44)
   c = dsn.0 + 1
   dsn.c = strip(ddw)
   dsn.0 = c
   lpa.c = strip(ddw)
   lpa.0 = c
   call listdsi "'"dsn.c"'"
   ext = ""
   sec = "     "
   if sysseconds > 0 then sec = "**2**"
   if sysextents > 1 then ext = "Ext:" sysextents
   dsn.c = left(dsn.c,50) sysvolume sec ext
   @EPLPS = D2X(X2D(@EPLPS)+45)              /* Bump to next 45 byte ent*/
   end

c = dsn.0 + 1
dsn.c = "   "
c = c + 1
dsn.c = "System LINK List"
c = c + 1
dsn.c = "---------------------------------------------------------"
c = c + 1
dsn.c = left("Data Set Name",50) "Volser"
dsn.0 = c

lnkext = 0

/* REXX - List the current LNKLST */
@CVT    = STORAGE(10,4)                     /* L R1,CVTPTR             */
@LLTA   = STORAGE(D2X(C2D(@CVT)+1244),4)    /* L R1,CVTLLTA-CVTMAP(R1) */
LNKC    = C2D(STORAGE(D2X(C2D(@LLTA)+4),4)) /* L R1,LLTCOUNT(R1)     */
@LLTA   = D2X(C2D(@LLTA)+8)                 /* LA R1,LLTENTRY(R1)      */
do i = 1 TO LNKC
   ddw = storage(d2x(x2d(@llta)+1),44)
   c = dsn.0 + 1
   call test_lpa strip(ddw)
   dsn.c = strip(ddw)
   dsn.0 = c
   call listdsi "'"dsn.c"'"
   ext = ""
   sec = "     "
   if sysseconds > 0 then sec = "**2**"
   if sysextents > 1 then ext = "Ext:" sysextents
   lnkext = lnkext + sysextents
   call test_lpa strip(ddw)
   dsn.c = left(dsn.c,50) sysvolume sec ext lpaflag
   @LLTA = D2X(X2D(@LLTA)+45)               /* Bump to next 45 byte ent*/
   end

  do i = 1 to dsn.0
     say dsn.i
     end

  say " "
  say "Total Linklist Extents:" lnkext

exit 0

Test_lpa:
arg linkdsn
do n = 1 to lpa.0
   if linkdsn = lpa.n then lpaflag = "lpa"
   return
   end
lpaflag = null
return
