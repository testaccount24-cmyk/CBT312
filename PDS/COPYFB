        /* --------------------  rexx procedure  -------------------- *
         * Name:      COPYFB                                          *
         *                                                            *
         * Function:  ISPF Edit Macro to Copy the current member      *
         *            into the predefined FB library.                 *
         *                                                            *
         *            This macro invokes the ISPF Edit REPLACE        *
         *            command to create or replace the member.        *
         *                                                            *
         * Syntax:    COPYFB                                          *
         *                                                            *
         * Author:    Lionel B. Dyck                                  *
         *                                                            *
         * History:                                                   *
         *            03/21/16 - Creation                             *
         *                                                            *
         * ---------------------------------------------------------- */
         Address ISREdit
         "Macro"

        /* --------------- *
         * Define defaults *
         * --------------- */
         target = "lionel.execfb"

        /* ------------------------------------------------- *
         * Test to see if there is any data beyond column 80 *
         * and if so let the user know before proceeding.    *
         * ------------------------------------------------- */
         "(lrecl) = LRECL"
         "find p'¬' First 81" lrecl-4
         if rc = 0 then do
            "Exclude All"
            "find p'¬' ALL 81" lrecl-4
            data = "The following records have data beyond"
            "line_before .zfirst = noteline '"data"'"
            data = "column 80 and must be corrected before"
            "line_before .zfirst = noteline '"data"'"
            data = "using this macro to copy the member."
            "line_before .zfirst = noteline '"data"'"
            exit 4
            end

        /* ---------------------------------------- *
         * Test to see if the member already exists *
         * ---------------------------------------- */
         "(member) = Member"
         newmem = target"("member")"

        /* ----------------------------------------------- *
         * Perform the Create or Replace using the Replace *
         * command which will create the member if it does *
         * not already exist.                              *
         * ----------------------------------------------- */
        "Replace" newmem .zfirst .zlast

        /* ------------------------------------------- *
         * Now change the userid for the copied member *
         * ------------------------------------------- */
        Address TSO,
        "Alloc f(sysut2) ds("target") shr reuse"
        Address ISPExec
        "LMINIT DATAID(DATAID) DDNAME(SYSUT2)"
        "LMOPEN DATAID("dataid")"
        "LMMSTATS DATAID("dataid") Member("member")",
                  "User(SYSLBD)"
        "LMCLOSE DATAID("dataid")"
        "LMFREE  DATAID("dataid")"
        Address TSO,
        "free f(sysut2)"
