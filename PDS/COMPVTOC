        /* --------------------  rexx procedure  -------------------- *
         * Name:      compvtoc                                        *
         *                                                            *
         * Function:  Compare dsn's on vtoc to catalog                *
         *                                                            *
         * Syntax:    %compvtoc volser                                *
         *                                                            *
         * Author:    Lionel B. Dyck                                  *
         *            Kaiser Permanente Information Technology        *
         *            Walnut Creek, CA 94598                          *
         *            (925) 926-5332                                  *
         *            Internet: lionel.b.dyck@kp.org                  *
         *                                                            *
         * History:                                                   *
         *            02/09/01 - creation                             *
         *                                                            *
         * ---------------------------------------------------------- */
         arg volser
         if length(volser) = 0 then do
            say 'Error: no volser provided.'
            say 'Ending........try again.'
            exit 8
            end
         temp = "temp.compvtoc"
         "Alloc f(sysin) ds("temp".sysin) new spa(1,1) tr",
            "Recfm(f b) lrecl(80) blksize(0) reuse"
         "Alloc f(sysprint) ds("temp".list) new spa(1,1) tr" ,
            "recfm(f b a) lrecl(121) blksize(0) reuse"
         "Alloc f("volser") unit(3390) vol("volser") reuse shr",
            "ds('sys1.linklib')"
         sysin.1 = " LISTVTOC VOL=3390="volser
         "Execio * diskw sysin (finis stem sysin."
         "Call 'sys1.linklib(iehlist)'"
         "Execio * diskr sysprint (finis stem list."
         "Free f(sysin sysprint) delete"
         "Free f("volser")"
         "Alloc f(sysin) ds(*)"
         "Alloc f(sysprint) ds(*)"
         if list.0 = 0 then do
            say "Error reading vtoc"
            say "Ending...."
            exit 8
            end
         say "Reporting on Volume:" volser "on system" mvsvar("sysname")
         say "All data sets on the volume and NOT in the Catalog" ,
             "will be reported."
         say " "
         do i = 1 to list.0
            if left(list.i,4) = " ---" then leave
            end
         do j = i+1 to list.0
            if left(list.j,1) = "1" then iterate
            if left(list.j,2) = "  " then iterate
            parse value list.j with 2 dsname .
            stat = sysdsn("'"dsname"'")
            if stat = "OK" then iterate
            say "dsname:" left(dsname,50)
            end
