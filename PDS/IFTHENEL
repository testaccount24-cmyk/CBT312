  /* --------------------  rexx procedure  -------------------- *
   | Name:      IfThenEl                                        |
   |                                                            |
   | Function:  ISPF Edit Macro to Display only REXX statements |
   |            with If, Then, Else, Do, Select, When, End      |
   |            and Otherwise.                                  |
   |                                                            |
   | Usage Notes: Enter on ISPF Edit or View command line       |
   |                                                            |
   | Dependencies: none                                         |
   |                                                            |
   | Author:    Lionel B. Dyck                                  |
   |                                                            |
   | History:  (most recent on top)                             |
   |            04/20/17 - Creation                             |
   |                       inspired by Bill Smith and           |
   |                       John Kalinich                        |
   * ---------------------------------------------------------- */
   Address ISREdit
   'Macro (options)'

   /* ---------------------------------------- *
    | First Reset and then Exclude ALL records |
    * ---------------------------------------- */
   'reset'
   'exclude all'

   /* ---------------------------------------------------- *
    | Now Find all the keywords that we are interested in. |
    * ---------------------------------------------------- */
   "find 'if' word all "
   "find 'then' word all "
   "find 'else' word all "
   "find 'do' word all "
   "find 'end' word all "
   "find 'select' word all "
   "find 'when' word all "
   "find 'otherwise' word all "

/* ---------------------------------------------------- *
 | Find all the start of comments and then find the     |
 | end of comment and exclude those records.            |
 |                                                      |
 | This hides any of the keywords that are in comments. |
 * ---------------------------------------------------- */
   opt = 'first'
   do forever
      "find '/*'" opt
      if rc > 0 then leave
      opt = 'next'
      '(hit) = cursor'
      "find '*/'"
      '(hitend) = cursor'
      'label' hit '= .a'
      if hit /= hitend then
      'label' hitend '= .b'
      if hit = hitend
      then 'exclude .a .a all'
      else 'exclude .a .b all'
   end
   'up m'
