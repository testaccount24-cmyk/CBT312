/* --------------------  rexx procedure  -------------------- *
 * Name:      FixNL                                           *
 *                                                            *
 * Function:  ISPF Edit Macro                                 *
 *                                                            *
 *            Correct files that have been downloaded from    *
 *            who knows and uploaded to z/OS with one very    *
 *            long record because the file only had newline   *
 *            characters without the carriage return to cause *
 *            a new record.                                   *
 *                                                            *
 *            The record is parsed and split at the newline   *
 *            x'25' character into unique records             *
 *                                                            *
 * Syntax:    FixNL                                           *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * History:                                                   *
 *            05/10/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 Address ISREdit
 'Macro'
 "(maxl) = linenum .zlast"
 "reset"
 r = 0
 nlc = x2c(25)
/* ------------------------------------------------- *
 * Loop thru all records but there typically is only *
 * one record to deal with.                          *
 * ------------------------------------------------- */
 do i = 1 to maxl
    '(data) = line' i
    do until length(data) = 0
       parse value data with record(nlc)data
       r = r + 1
       out.r = record
       end
    end
 do i = 1 to r
    line = out.i
    "Line_Before .zlast = (line)"
    end
/* ----------------------------------------------------- *
 * Delete the last record as that is the original record *
 * that is too long and does not belong here :-)         *
 * ----------------------------------------------------- */
 "Delete .zlast"
