/* --------------------  rexx procedure  -------------------- *
 * Name:      cronlog                                         *
 *                                                            *
 * Function:  browse the cron log                             *
 *                                                            *
 * Syntax:    %cronlog                                        *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * Dependencies:  (1) superuser                               *
 *                (2) stemview                                *
 *                                                            *
 * Notes:     This exec and panel could be changed to work    *
 *            with any log file                               *
 *                                                            *
 * History:                                                   *
 *            06/06/16 - creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 logtbl = 'cron'random(999)
 panel  = 'cronlogt'
 dir = '/usr/spool/cron'

/* -------------------------------------------------- *
 * Get current uid and change to uid(0) to start with *
 * -------------------------------------------------- */
 address syscall 'geteuid'
 myeuid=retval
 Address syscall "seteuid 0"

/* -------------------------------------------------- *
 * List the target directory and return to normal uid *
 * -------------------------------------------------- */
 option = 'ls -la' dir
 rc = bpxwunix(option,,stdout.,stderr.)
 Address syscall "seteuid" myeuid

/* ------------------------------------------------ *
 * Setup ISPFExec environment and create ISPF Table *
 * ------------------------------------------------ */
 Address ISPExec
 'TBCreate' logtbl 'Names(zsel date time size file)'

/* ---------------------------------------- *
 * Process the directory list for log files *
 * ---------------------------------------- */
 do i = 1 to stdout.0
 parse value stdout.i with w1 x id grp size mm dd time file
 date = mm dd
    if left(w1,1) = 'd' then iterate
    if w1 = 'total' then iterate
    if pos('log',file) = 0 then iterate
    file = dir'/'file
    'tbadd' logtbl
    end

/* ------------------------------------- *
 * Now display the ISPF Table of Members *
 * ------------------------------------- */
 crp = 0
 rowcrp = crp
 do forever
    zcmd    = null
    if src = 4 then "TBDispl" logtbl
       else do
            "TBTOP" logtbl
            "TBSKIP" logtbl "NUMBER("crp")"
            if rowcrp = 0 then
               "TBDISPL" logtbl "PANEL("panel")"
               else
               "TBDISPL" logtbl "PANEL("panel")",
                       "CSRROW("rowcrp") AUTOSEL(NO)"
            end
    src = rc
    if src > 4 then leave

/* ---------------------------------------------------------- *
 * Process the member selections:                             *
 *   Commands: B - Browse                                     *
 *             S - Browse                                     *
 *             V - View                                       *
 * ---------------------------------------------------------- */
 if ztdsels > 0 then do
 if zsel = 'S' then zsel = 'B'
 Select
   /* ------ *
    * Browse *
    * ------ */
    when zsel = 'B' then do
         'Control Display Save'
         opt = 'browse'
         call do_it
         'Control Display Restore'
         end
   /* ------ *
    * View   *
    * ------ */
    when zsel = 'V' then do
         'Control Display Save'
         opt = 'view'
         call do_it
         'Control Display Restore'
         end
    Otherwise nop
    end
 end
 end

/* ---------------------------- *
 * Close out the table and exit *
 * ---------------------------- */
 'TBEnd' logtbl
 exit 0

/* -------------------------------------------------- *
 * Now that we know which log file to process we will *
 * - change uid to 0 (superuser)                      *
 * - cat the file and capture the output              *
 * - return to our normal uid                         *
 * - use stemview to browse or view the results       *
 * -------------------------------------------------- */
 Do_It:
 Address syscall "seteuid 0"
 option = 'cat' file
 rc = bpxwunix(option,,stdout.,stderr.)
 Address syscall "seteuid" myeuid
 if stdout.0 > 0 then
 CALL stemview opt,'stdout.',,,"cron log" opt file
 if stderr.0 > 0 then
    CALL stemview opt,'stderr.',,,"cron log stderr" opt file
 return
