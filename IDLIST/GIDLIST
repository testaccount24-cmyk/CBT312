/*%NOCOMMENT====================* REXX *==============================*/
/*  PURPOSE:  Display USS GID List                                    */
/*--------------------------------------------------------------------*/
/* FLG  YYMMDD  USERID   DESCRIPTION                                  */
/* ---  ------  -------  -------------------------------------------- */
/* @LD  160519  LBD      Change to use Stemview for viewing           */
/* @A2  160323  R2221    Removed checking ZSCTPREF variable for WFS   */
/* @A1  110910  R2221    Allow executing on mutiple lpars/split scrs  */
/* @A0  110909  S54017   Created REXX                                 */
/*====================================================================*/
X        = MSG("OFF")

if syscalls('ON')>3 then do
    say 'Unable to establish the SYSCALL environment'
    return
end

row = 0                                                         /* @LD */

do forever
  address syscall "getgrent gr."
  if retval=0 | retval=-1 then
    leave
  gid=left(gr.2,8)
  row = row + 1                                                 /* @LD */
  t.row = "Group-Name="gr.1 "GID="gid "#members="gr.3           /* @LD */
  #m=gr.3
  do i=4 to #m+3 by 7
     do j=i to i+6
        if j>#m+3 then gr.j=''
     end
     l=i+1; m=i+2; n=i+3; o=i+4; p=i+5; q=i+6
     row = row + 1                                              /* @LD */
     t.row = "    "gr.i gr.l gr.m gr.n gr.o gr.p gr.q           /* @LD */
  end
  row = row + 1                                                 /* @LD */
  t.row = " "                                                   /* @LD */
end
t.0 = row

if sysvar('sysispf') = 'ACTIVE' then
   CALL stemview "View",'t.',,,"GIDList"                       /* @LD */
else do i = 1 to t.0
     say t.i
     end
