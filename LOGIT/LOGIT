        /* --------------------  rexx procedure  -------------------- */
           ver = "0.5"
        /* Name:      Logit                                           *
         *                                                            *
         * Function:  Log a message into a log data set.              *
         *                                                            *
         *            Designed to be called by a CLIST or REXX Exec   *
         *            to log items of interest.                       *
         *                                                            *
         *            The log data set should never be processed      *
         *            interactively. It should be copied and the      *
         *            copy reviewed.                                  *
         *                                                            *
         *            Note that the log data set must be defined      *
         *            such that everyone can write into it.           *
         *                                                            *
         *            The log message will be written with a          *
         *            header:                                         *
         *            yyyymmdd hh:mm:ss jobname userid systemid       *
         *               message text                                 *
         *                                                            *
         *            It is recommended that the log data set be      *
         *            allocated RECFM=VB LRECL=255 BLKSIZE=0          *
         *            with a large primary and a secondary in tracks  *
         *            or blocks as the calculation for free space     *
         *            is based on allocated-used space from listdsi   *
         *            so the more granular the better.                *
         *                                                            *
         *            The test for free space requires at least 2     *
         *            free space units (blocks or tracks).            *
         *                                                            *
         *            See the LOGITCPY to copy and clear the log.     *
         *                                                            *
         * NOTE:      If the Log Data Set has no remaining space      *
         *            then there will be no attempt to write the      *
         *            log entry and this exec will return with        *
         *            a return code of 4.                             *
         *                                                            *
         * Syntax:    %logit log_data_set log_message_text            *
         *                                                            *
         *            The log_data_set must be pre-allocated and      *
         *            if accessed using DISP=MOD. If the data set     *
         *            can not be allocated because it is in use       *
         *            then it is attempted n more times before        *
         *            giving up (where n is defined in this code.     *
         *                                                            *
         *            The log_message text may be mixed case and      *
         *            does not require any quotes or other special    *
         *            formatting.                                     *
         *                                                            *
         * Author:    Lionel B. Dyck                                  *
         *            Kaiser Permanente Information Technology        *
         *            Walnut Creek, CA 94598                          *
         *            (925) 926-5332                                  *
         *            Internet: lionel.b.dyck@kp.org                  *
         *                                                            *
         * History:                                                   *
         *            04/12/04 - 0.5 Correct setting of Sleep var     *
         *            04/03/04 - 0.4 Change to require a minimum of   *
         *                       2 free space units                   *
         *            04/02/04 - 0.3 Change Exit to Return            *
         *                       Test if there is space available     *
         *                       in the log data set before writing   *
         *                       to avoid a D37 abend                 *
         *            04/02/04 - Add version and change jobname/userid*
         *                       to 8 chars in length                 *
         *            04/02/04 - Correct call to Sleep routine        *
         *            04/02/04 - Improve comments and add header      *
         *            04/01/04 - Creation                             *
         *                                                            *
         * ---------------------------------------------------------- */
        parse arg logds logmsg

        /* ---------------------------- *
         * Test that we have both parms *
         * ---------------------------- */
         if logds = "" then do
            say "Logit: Error - invalid syntax"
            say "       No parameters provided."
            say "       Exiting."
            return 8
            end
         if logmsg = "" then do
            say "Logit: Error - invalid syntax"
            say "       No log message text provided."
            say "       Exiting."
            return 8
            end

        /* ------------------------------------ *
         * Determine if the log data set exists *
         * ------------------------------------ */
         call listdsi logds
         if sysreason > 0 then do
            say "Logit: Error - the log data set:" logds
            say "      " sysdsn(logds)
            say "        Exiting...."
            return 8
            end
         if sysalloc - sysused < 2 then return 4

        /* ------------------------------------------------------ *
         * Define the number of seconds to sleep between attempts *
         * ------------------------------------------------------ */
         sleep = 1

        /* ----------------------------------- *
         * Define the number of retry attempts *
         * ----------------------------------- */
         retry = 10

         dd = "logit"random(999)
         c  = 1
         call msg "off"

         do forever
            c = c + 1
            if c > retry then return 4
            "alloc f("dd") mod ds("logds") reuse"
            if rc > 0 then call sleep
            else do
                 queue date('s') time('n') ,
                       left(mvsvar("symdef","jobname"),8) ,
                       left(sysvar('sysuid'),8) ,
                       mvsvar('sysname') ,
                       logmsg
                 "Execio 1 diskw" dd "(finis"
                 "Free f("dd")"
                 leave
                 end
            end

        /* --------------------------------------------------- *
         * Sleep Routine. This routine will call the USS Sleep *
         * routine and sleep for 1 second.                     *
         * --------------------------------------------------- */
         Sleep:
         call syscalls('ON')           /* allow calls to omvs */
         address 'SYSCALL' 'SLEEP ('sleep')'
         return
