        /* --------------------  rexx procedure  -------------------- *
         * Name:      SXMITLOG                                        *
         *                                                            *
         * Function:  Save the XMITIP Log data set to a copy          *
         *            and clear by calling LOGITCPY                   *
         *                                                            *
         * Syntax:    %sxmitlog logdsn save_hlq                       *
         *                                                            *
         *            logdsn must be fully qualified                  *
         *                   (e.g. 'sys1.xmitip.log')                 *
         *            save_hlq must be a valid hlq without            *
         *            qualifiers                                      *
         *                   (e.g. sys1.xmitip.savelog)               *
         *                                                            *
         * Customizations:                                            *
         *            1. The notify variable to e-mail the            *
         *               log                                          *
         *                                                            *
         * Author:    Lionel B. Dyck                                  *
         *            Kaiser Permanente Information Technology        *
         *            Walnut Creek, CA 94598                          *
         *            (925) 926-5332                                  *
         *            Internet: lionel.b.dyck@kp.org                  *
         *                                                            *
         * History:                                                   *
         *            04/05/04 - Creation                             *
         *                                                            *
         * ---------------------------------------------------------- */
         arg logdsn save_hlq

        /* ------------------------- *
         * Test for input parameters *
         * ------------------------- */
         if logdsn =  "" then call invalid_syntax
         if save_hlq= "" then call invalid_syntax

        /* ---------------------- *
         * Test for logdsn status *
         * ---------------------- */
         if sysdsn(logdsn) <> "OK" then do
            say "Invalid logdsn provided:" logdsn
            say sysdsn(logdsn)
            say "Exiting...."
            exit 8
            end

        /* --------------------------------- *
         * Define the Save Log Data Set Name *
         * --------------------------------- */
         save_dsn =  "'"save_hlq".D"date('j')"'"

        /* ---------------------------------------------------- *
         * *Custom*                                             *
         * Define the Notify e-mail address and the From e-mail *
         * address.                                             *
         * ---------------------------------------------------- */
         notify = "lionel.b.dyck@kp.org"
         from   = "lionel.b.dyck@kp.org"

        /* ------------------------------------------ *
         * Now call LOGITCPY to do the copy and clear *
         * ------------------------------------------ */
         "%logitcpy" logdsn save_dsn "clear"

         '%xmitip' notify 'subject "xmitip log created"' ,
            'msgt "The XMITIP Log data set' logdsn 'has been' ,
            'copied to' save_dsn 'and cleared."' ,
            'From' from ,
            'File' save_dsn 'Format rtf/l/9/let' ,
            'Filename xmitip.log.d'date('j')'.rtf'

        /* ----------------------------- *
         * All done so exit this routine *
         * ----------------------------- */
         exit 0

        /* ------------------------------ *
         * Report Invalid Syntax and Exit *
         * ------------------------------ */
         Invalid_Syntax:
         Say "SXMITLOG has been called with invalid syntax:"
         say "   %sxmitlog logdsn save_hlq"
         say " "
         say "Try again...."
         exit 8
