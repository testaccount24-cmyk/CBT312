  /*%NOcomment ----------  rexx procedure  -------------------- *
  | Name:      ALLOCADD                                        |
  |                                                            |
  | Function:  Re-Allocate the specified DD adding the         |
  |            requested dataset(s) to the front (F) or        |
  |            back (B - default) of the allocation            |
  |                                                            |
  | Syntax:    %allocadd ddname dsn1 dsn2 dsn3 ... dsn# \ opt  |
  |                                                            |
  |            opt is B to place last otherwise the dsns are   |
  |            allocated in before existing allocations        |
  |                                                            |
  | Usage Notes: If the ddname is not allocated it will be     |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            08/23/20 LBD - Change default to before         |
  |                         - Detect if requested DD is open   |
  |                           and may not be changed           |
  |            08/22/20 LBD - Refine and make it really work   |
  |            08/21/20 LBD - Creation                         |
  |                                                            |
  * ---------------------------------------------------------- *
  | Copyright (C) 2020 by Lionel B. Dyck under GPLV3           |
  * ---------------------------------------------------------- */
  arg ddname dsname '\' opt

  /* --------------------------- *
  | Get the name of the command |
  * --------------------------- */
  parse source . . name .

  /* ----------------------------- *
  | Validate (somewhat) the input |
  * ----------------------------- */
  if ddname = '' then bad = 1
  if dsname = '' then bad = 1
  if pos('.',ddname) > 1 then bad = 1
  if opt /= '' then if opt /= 'B' then bad = 1

  if bad = 1 then do
    say 'Concatenation command failed due to invalid syntax.'
    say ' '
    say 'Correct syntax is:'
    say ' '
    say '%'name 'ddname dsname(s) \ option'
    say '        option is B for last'
    say '                  or default is first'
    say ' '
    exit 8
  end

  /* -------------------------------------------- *
  | Get the list of currently allocated datasets |
  * -------------------------------------------- */
  dsnames = get_allocations(ddname)

  if dsnames = 0 then do
    new_alloc = 0
    dsnames = ''
  end
  else new_alloc = 1

  /* --------------------------------------- *
  | Process each Dataset Name one at a time |
  | to fixup and add to the allocation list |
  | in reverse order so they add in order   |
  * --------------------------------------- */
  do id = words(dsname) to 1 by -1
    dsn = word(dsname,id)
    call fix_dsn
    if opt = 'B'
    then dsnames = dsnames dsn
    else dsnames = dsn dsnames
  end

  /* -------------- *
  | Now reallocate |
  * -------------- */
  if new_alloc = 1 then do
    call outtrap 'x.'
    'Free file('ddname')'
    frc = rc
    call outtrap 'off'
    if rc > 0 then do
      say name 'failed because the requested dd is open and thus' ,
        'may not be changed.'
      exit 8
    end
  end
  'Alloc f('ddname') shr ds('dsnames') reuse'
  exit

  /* ------------------------------------------------ *
  | Fixup the provided dataset name to fully qualify |
  * ------------------------------------------------ */
Fix_DSN:
  x = listdsi(dsn)
  if x > 0 then do
    say dsn sysmsglvl2
    exit 8
  end
  dsn = "'"sysdsname"'"
  return

  /* ------------------------------------ *
  | Get the currently allocated datasets |
  * ------------------------------------ */
Get_Allocations: Procedure
  arg cdd

  call outtrap 'trap.'
  "lista sta"
  call outtrap 'off'

  parse value '' with dd tdd dsns

  do i = 1 to trap.0
    if left(trap.i,1) = '-' then iterate
    if word(trap.i,1) /= 'TERMFILE' then
    if left(trap.i,1) /= ' ' then dsns = dsns "'"word(trap.i,1)"'"
    if word(trap.i,1) = 'KEEP' then iterate
    if left(trap.i,1) = ' ' then do
      dd = word(trap.i,1)
      if tdd = '' then do
        tdd = dd
        iterate
      end
      if tdd = cdd then do
        return subword(dsns,1,words(dsns)-1)
        exit
      end
      dsns = word(dsns,words(dsns))
      tdd = dd
    end
  end
  if tdd = cdd then return subword(dsns,1,words(dsns))
  return 0
