        /* this rexx exec is expected to be invoked by the EXEC command
           thus:

           EXec 'dsname(RECEIVE)'

           Prompting will occur for hlq and optional volser.

           and it will then issue the TSO RECEIVE command for the
           the following members to create these new data sets:

           Member       Dataset
           EXEC         hlq.EXEC and
           PANELS       hlq.PANELS

           You will then need to copy these datasets/members into
           datasets for production use.

           After these data sets are created several of the members
           of this install data set will be browsed.
           */

        parse source x1 x2 x3 x4 dsn .
         x = PROMPT("ON") /* enable prompting */

        say "Enter desired hlq for target data sets",
            "(default is" sysvar("syspref")".CAVXTRT.NEW):"
        pull hlq
        hlq = strip(hlq)

        if hlq = "" then hlq = sysvar("syspref")".CAVXTRT.NEW"

        say "Enter optional volser for target data sets",
            "default is to allow standard allocation to",
            "find a volume."
        pull volser
        volser = strip(volser)

        if volser <> "" then vol = "vol("volser")"
                        else vol = ""

        Say "Using HLQ:" hlq
        if vol <> "" then
           Say "Using Volser:" volser

        queue "dsn('"hlq".exec')" vol
        "Receive inds('"dsn"(exec)')"
        queue "dsn('"hlq".panels')" vol
        "Receive inds('"dsn"(panels)')"

        /* ------------- *
         * build tryexec *
         * ------------- */
         edsn = "'"hlq".exec'"
         pdsn = "'"hlq".panels'"
         t.1 = "/* REXX - Sample for testing */"
         t.2 = '"altlib activate application(exec) dataset('edsn')"'
         t.3 = "address ispexec"
         t.4 = '"libdef ispplib dataset id('pdsn') stack"'
         t.5 = "'select cmd(%cavxtrt) newappl(isr) passlib'"
         t.6 = "address tso"
         t.7 = "'altlib deactivate application(exec)'"
         t.8 = "address ispexec"
         t.9 = "'libdef ispplib'"
         t.0 = 9

        "Alloc f(try) ds('"dsn"(tryexec)') shr"
        "Execio * diskw try (finis stem t."
        "Free f(try)"

        if sysvar('sysispf') <> "ACTIVE" then exit
        Address ISPEXEC
           "Browse dataset('"dsn"($doc)')"
           "Browse dataset('"dsn"(changes)')"
           "Browse dataset('"dsn"($notes)')"
           "Browse dataset('"dsn"($install)')"
           "Browse dataset('"dsn"(annsamp)')"
           "Browse dataset('"dsn"(logic)')"
           "Browse dataset('"dsn"(tryexec)')"

