/* --------------------  rexx procedure  -------------------- *
| Name:      Align                                           |
|                                                            |
| Function:  ISPF Edit Macro to Align the text on the        |
|            specified line or lines                         |
|                                                            |
|            Options:                                        |
|            ? display help                                  |
|            C ll - strip and center                         |
|            F ll - flow text to line length(ll)             |
|            L - strip and left justify                      |
|            R - strip and right justify                     |
|            V - strip and reverse text                      |
|                                                            |
|            Optional for C is ll for line length            |
|            e.g. ALIGN C 65                                 |
|                 To align center for a width of 65          |
|                                                            |
| History:  (most recent on top)                             |
|            12/31/19 LBD - Set cursor in command line       |
|            02/14/19 LBD - Move RESET and add Flow          |
|            02/13/19 LBD - Cleanup                          |
|            02/12/19 LBD - creation                         |
|                                                            |
* ---------------------------------------------------------- */
  Address ISREdit
  'Macro (options) NOPROCESS'
  options = translate(options)
  if options = '' then options = 'C'
  if options = '?' then call do_help
  '(dw) = data_width'
  opt = word(options,1)
  if word(options,1) = 'F' then do
    flowll = word(options,2)
    if datatype(flowll) /= 'NUM' then call bad_flow
    if flowll > dw then call bad_flow
    fc = 0
  end
  if words(options) > 1 then
  if word(options,1) = 'C' then do
     dwt = word(options,2)
     if datatype(dwt) /= 'NUM' then call bad_num
     if dwt > dw then call bad_num
     dw = dwt
     end
  Address ISPExec 'Control Errors Return'
  'Reset Special'
  "PROCESS RANGE C"
  "(start) = linenum .zfrange"
  "(stop) = linenum .zlrange"
  do i = start to stop
    '(data) = line' i
    Select
      when opt = 'C' then do
        if length(strip(data)) > dw then do
           msg = 'Line data exceeds center length - ignoring center'
           "Line_After" i "= msgline (msg)"
           act = 0
           end
        else do
        data = center(strip(data),dw)
        act = 'centered'
      end
      end
      when opt = 'L' then do
        data = strip(data)
        act = 'left justified'
      end
      when opt = 'R' then do
        data = right(strip(data),dw)
        act = 'right justified'
      end
      when opt = 'V' then do
        data = reverse(data)
        act = 'reversed'
      end
      when opt = 'F' then do
        act = 'Flowed to' flowll
        'tflow' i flowll
        "(stop) = linenum .zlrange"
        fc = fc + 1
      end
      Otherwise nop
    end
    'line' i '= (data)'
  end

  if word(options,1) = 'F' then
  text = fc 'lines' act 'with a width of' dw+0
  else if act /= 0 then do
  text = (stop - start) +1 'lines' act 'with a width of' dw+0
  'line_before' start '= msgline (text)'
  end
  Exit  1

/* ---------------------------- *
| Display the Help information |
* ---------------------------- */
Do_Help:
  zedsmsg = ''
  zedlmsg = left('Align Edit Macro options:',76),
    left('C ll-Center F ll-Flow L-Left R-Right',
    'V-reVerse',76),
    left('Leading and Trailing blanks',
    'are always removed except for' ,
    'reVerse and Flow.',76) ,
    left('Note ll (line length) is optional for C',76),
    left('----',76),
    left('F 72 will flow all records to column 72',76)
  Address ISPExec 'Setmsg msg(isrz001)'
  exit
/* ------------------------------ *
 | Display info on the Bad Number |
 | for options C, L, and R        |
 * ------------------------------ */
Bad_num:
  Address ISPExec
  zedsmsg = 'Invalid Length'
  zedlmsg = left('C may be followed by a valid column number',76),
    left('e.g. C 72 to center on width of 72',76),
    left('It must never exceed the data width of' dw+0,76),
    left('.',76),
    left('Current Length specification is:' options,76)
  'Setmsg msg(isrz001)'
  exit

/* ---------------------------------- *
| Display info on bad flow parameter |
* ---------------------------------- */
Bad_flow:
  Address ISPExec
  zedsmsg = 'Invalid Flow'
  zedlmsg = left('Flow must be followed by a valid column number',76),
    left('e.g. F 72 to flow to column 72',76),
    left('It must never exceed the data width of' dw+0,76),
    left('.',76),
    left('Current Flow specification is:' options,76)
  'Setmsg msg(isrz001)'
  exit
